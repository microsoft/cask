# CASK 256-bit Primary Keys
## Standard Backus-Naur Form (BNF)
```
<key> ::= <random-data> <key-size> <cask-signature> <provider-signature> <provider-kind> <cask-kind> <correlating-id> <timestamp> <expiry> [<optional-fields>] 
<random-data> ::= 42 * <base64url> <base64-two-zeros-suffix> ; The total random data comprises 256 bits encoded as 42
                                                             ; characters x 6 bit bits of random data = 252 bits and
                                                             ; 1 character providing 4 bits of random data padded with 00b.
<key-size> ::= 'A' ; 'A' indicates a 256-bit key.
<cask-signature> ::= 'JQQJ' ; Fixed signature identifying the CASK key
<provider-signature> ::= 4 * <base64url> ; Provider identifier (24 bits)
<provider-kind> ::= <base64url> ; Optionally populated provider key kind.
<cask-kind> ::= <256-bit-key> |  <256-bit-hash> | <384-bit-hash>
<256-bit-key> ::= 'A'
<256-bit-hash> ::= 'H'
<384-bit-hash> ::= 'I'
<correlating-id> ::= <four-bits-prefixed-base64url> 21 * <base64url> ; 0000b + prefix 22 character correlating id (128 bits)
<timestamp> ::= <year> <month> <day> <hour> <minute>; Timestamp components
<year> ::= <base64url> ; Represents the year, 'A' (2024) to '_' (2087)
<month> ::= 'A'..'L' ; For months January to December
<day> ::= 'A'..'Z' | 'a'..'e' ; 'A' = day 1, 'B' = day 2, ... 'e' = day 31
<hour> ::= 'A'..'X' ; Represents hours 0-23. 'A' = hour 0 (midnight), ... 'X' = hour 23.
<minute> ::= 'A'..'7' ; Represents minutes 0-59.
<expiry> ::= 3 * <base64url> ; 18 bits expiry data, comprising a count of 5 minute values.
                             ; 64^3 = 262,144 possible values, allowing for a maximum
                             ; expiry of 5 * 262,143 minutes (~910 days).
                             <base64url> ::= 'A'..'Z' | 'a'..'z' | '0'..'9' | '-' | '_'
<optional-fields> ::= { <optional-field> } ; Zero or more 4-character (24 bit) sequences of optional data.
<optional-field> ::= 4 * <base64url> ; Each optional field is 4 characters (24 bits). This keeps
                                     ; data cleanly aligned along 3-byte/4-encoded character boundaries
                                     ; facilitating readability of encoded form as well as byte-wise use.
<four-bits-prefixed-base64url> ::= 'A' | 'B' | 'C' | 'D'; Base64 indices that begin with 0000b.
<base64-two-zeros-suffix> ::= 'A' | 'E' | 'I' | 'M' | 'Q' | 'U' | 'Y' | 'c' ; Base64 characters ending in 00b. These indices are all
                            | 'g' | 'k' | 'o' | 's' | 'w' | '0' | '4' | '8' ; multiple of 4 (or the value of 0b), a fact that may be
                                                                            ; useful in some contexts.
```
## Byte-wise Rendering
|Byte Range|Decimal|Hex|Binary|Description|
|-|-|-|-|-|
|decodedKey[..31]|0...255|0x0...0xFF|00000000b...11111111b|256 bits of random data produced by a cryptographically secure RNG|
|decodedKey[32]|0|0x00|00000000b| 2 bits of reserved padding + the key size.
|decodedKey[33..36]| 37, 4, 9  |0x25, 0x04, 0x09| 00100101b, 00000100b, 00001001b | Decoded 'JQQJ' signature.
|decodedKey[36..39]|0...255|0x0...0xFF|00000000b...11111111b| Provider identifier, e.g. , '0x4c', '0x44', '0x93' (base64 encoded as 'TEST')
|decodedKey[39]|||| 6-bit provider key kind + 2 bits of reserved padding.
|decodedKey[40]|||| 4-bit CASK provider key kind + 4 bits padding.
|decodedKey[41..57]||||16 byte non-sensitive, unique correlating id.
|decodedKey[57..60]||||Time stamp data encoded in 4 six-bit blocks for YMDH.
|decodedKey[60..63]||||Time stamp data encoded in 4 six-bit blocks for MEEE (Minute, 24 bits expiry)
|decodedKey[63..]|0...255|0x0...0xFF|00000000b...11111111b|Provider-defined data, comprising 0 or more 3-byte sequences, of arbitrary interpretation.

## Primary 256-bit Key Base64-Encoded Rendering
|String Range|Text Value|Description|
|-|-|-|
|encodedKey[..42] | 'A'...'_' | 252 bits of randomized data generated by cryptographically secure RNG
|encodedKey[42] | <base64-two-zeros-suffix> | 4 bits of randomized data followed by 2 zero bits. See the <base64-two-zeros-suffix> definition for legal values.
|encodedKey[43] | 'A' | The 6-bit encoded sensitive component size.
|encodedKey[44..48]|'JQQJ'| Fixed CASK signature.
|encodedKey[48..52]|('A'...'Z'-\_)\|('a'...'z'-\_)| | The provider signature. Optionally, this data can be encoded to distinguish user- vs. service-managed keys.
|encodedKey[52]|'A'...'_'|Provider key kind or 'A' if not populated|
|encodedKey[53]|'A', 'H'|CASK key kind|
|encodedKey[54]|'A', 'Q', 'g', 'w'|The first character of the correlating id|
|encodedKey[55..76]|'A'...'_'|The remaining 21 characters of the correlating id|
|encodedKey[76]|'A'...'_'|Represents the year of allocation time, 'A' (2024) to '_' (2087)|
|encodedKey[77|'A'...'L'|Represents the month of allocation time, 'A' (January) to 'L' (December)|
|encodedKey[78]|'A'...'Z'\|'a'..'e'|Represents the day of allocation time, 'A' (0) to 'e' (31)|
|encodedKey[79]|'A'...'X'|Represents the hour of allocation time, 'A' (hour 0 or midnight) to 'X' (hour 23).
|encodedKey[80]|'A'...'7'| Represents the minute of allocation time.
|encodedKey[81..84]|'A'...'_'| 18-bit value comprising an expiry.
|encodedKey[84..]|'A'...'_'|0 or more 4-character sequences comprising provider optional data, of arbitrary interpretation.

```
